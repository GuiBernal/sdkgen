apply plugin: 'com.jfrog.bintray'

version = '1.0.0'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
//    archives javadocJar
    archives sourcesJar
}

Properties credentials
if (System.getenv('BINTRAY_USER') == null
        && project.rootProject.file('credentials.properties').exists()) {
    credentials = new Properties()
    credentials.load(new FileInputStream(project.rootProject.file('credentials.properties')))
}

// Bintray
bintray {
    if (credentials != null
            && credentials['bintray.user'] != null
            && credentials['bintray.apikey'] != null) {
        user = credentials['bintray.user']
        key = credentials['bintray.apikey']
    } else if (System.getenv('BINTRAY_USER') != null && System.getenv('BINTRAY_API_KEY') != null ) {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')
    } else {
        user = ''
        key = ''
    }

    pkg {
        repo = 'sdkgen'
        name = 'sdkgen-android-runtime'
        configurations = ['archives']
        desc = 'Sdkgen\'s Android Runtime.'
        websiteUrl = 'https://github.com/sdkgen/sdkgen'
        vcsUrl = 'https://github.com/sdkgen/sdkgen.git'
        licenses = ["Apache-2.0"]
        publish = true
        override = false
        publicDownloadNumbers = true
    }
}